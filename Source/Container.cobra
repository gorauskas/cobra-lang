interface IContainer
	inherits IMember
	"""
	Containers are named nodes that respond to declForName().
	Containers include:
		* NameSpace
		* Box
		  * Class
		  * Struct
		  * Interface
		* Enum
	"""
	def declForName(name as String) as IMember?
		"""
		Returns the member of this container with the given name.
		May return nil if no such container exists.
		Case-sensitive.
		"""

	def memberForName(name as String) as IMember?
		"""
		Returns the member of this container with the given name, including any inherited members.
		May return nil if no such container exists.
		Case-sensitive.
		"""


class Container
	inherits NamedNode
	implements IContainer, IType
	"""
	TODO: Make Container a generic class. class Container<of TMember> where TMember inherits INamedNode

	TODO: Should Container inherit from CobraType? I guess the problem is that NamedNode is a
	SyntaxNode and CobraType inherits from Node. However, IType does specify a name... Maybe
	NamedNode should inherit Node and the token thing done via ISyntaxNode only. At the very
	least, greatestCommonDenominatorWith() is duplicated. Others may be as well.

		More thoughts on this. NameSpace inherits from Container, but NameSpace is *not* a type.
		Also, there are several members here that are duplicated from CobraType.
		I wish .NET had natural support for multiple inheritance
		So my current thoughts:
			Container should not implement IType
			Box should implement IType
			Live with some duplication for now
			Later, move duplicated members to the IType interface when Cobra supports interface code
			
	A container has:
		* declarations in a given order, but also accessible by name,
		  both case-sensitive and non.
		* a doc string

	A container is a NamedNode, IContainer and IType.

	The declForName[CI] methods do not follow inheritance, but the
	memberForName() should (subclasses have to override).

	IMember is implemented (brought in via IContainer).

	The target type that is contained is IMember.  TODO: should that be a generic parameter?

	Subclasses are responsible for passing bindInt and bindImp to _declsInOrder.
	"""

	var _parentNameSpace as NameSpace?
	var _parentBox as Box?
	
	var _declsInOrder as List<of IMember>  # TODO: can the inner list type be narrowed more? say to IClassMemberDecl or ClassMemberDecl
	var _declsByName as Dictionary<of String, IMember>
	var _declsByNameCI as Dictionary<of String, IMember>
	var _docString as String
	var _isNames as List<of String>

	def init(token as IToken, name as String, isNames as List<of String>, docString as String?)
		.init(nil, token, name, isNames, docString)

	def init(parent as Container?, token as IToken, name as String, isNames as List<of String>, docString as String?)
		require parent implies (parent inherits NameSpace or parent inherits Box)
		base.init(token, name)
		if parent
			if parent inherits NameSpace
				# _parentNameSpace = parent
				# don't set the parentNameSpace because NameSpace.addDecl does that *and* there it will substitute its .unifiedNameSpace
				pass
			else if parent inherits Box
				_parentBox = parent
			else
				throw FallThroughException(parent)
		_declsInOrder = List<of IMember>()
		_declsByName = Dictionary<of String, IMember>()
		_declsByNameCI = Dictionary<of String, IMember>()
		_isNames = isNames
		_docString = docString ? ''

	get declsInOrder from var

	pro parentNameSpace from var
		"""
		The namespace this container was declared inside of, if any.
		If nil, the .parentBox should not be.
		"""

	pro parentBox from var
		"""
		The box this container was declared inside of, if any.
		Consider, for example, an enumeration that is declared inside a class.
		If nil, the .parentNameSpace should not be.
		"""

	get parent as Container?
		if .parentBox
			return .parentBox
		if .parentNameSpace
			return .parentNameSpace
		return nil

	def addDecl(decl as IMember)
		require
			decl.name.length
			not .declsInOrder.contains(decl)
			.declForName(decl.name) is nil
			.declForNameCI(decl.name) is nil
		ensure
			.declsInOrder.contains(decl)
			.declForName(decl.name) is decl
			.declForNameCI(decl.name) is decl
			.declsInOrder.count == old .declsInOrder.count + 1
		body
			_declsInOrder.add(decl)
			_declsByName.add(decl.name, decl)
			_declsByNameCI.add(decl.name.toLower(), decl)

	def declForName(name as String) as IMember?
		require
			name.length
		ensure
			result implies result.name==name
		body
			return if(_declsByName.containsKey(name), _declsByName[name], nil)

	def declForNameCI(name as String) as IMember?
		require
			name.length
		ensure
			result implies result.name.toLower()==name.toLower()
			result is nil implies .declForName(name) is nil
		body
			name = name.toLower()
			return if(_declsByNameCI.containsKey(name), _declsByNameCI[name], nil)

	pro docString from var

	def addSubFields is override
		base.addSubFields()
		.addField('declsInOrder', _declsInOrder)

	def dumpDeclNames
		print
		print this
		for decl in _declsInOrder
			print '  [decl.name] --> [decl.getType.name]'


	## IMember

	get englishName as String
		throw OverrideException(.getType)
		return ''

	get isCallable as bool
		return false

	get requiresThis as bool
		return false

	get resultType as IType
		return this

	## IType

	def greatestCommonDenominatorWith(type as IType) as IType
		# TODO: the duplication with CobraType here feels wrong
		if this is type
			return this
		if type inherits NilableType
			return type.greatestCommonDenominatorWith(this)
		if type inherits NilType
			return NilableType(this)
		if .isDescendantOf(type)
			return type
		if type.isDescendantOf(this)
			return this
		# TODO: following is a guess
		if .superType and type.superType
			return .superType.greatestCommonDenominatorWith(type.superType to IType)
		else
			return .compiler.libraryType('System', 'Object')

	def isAssignableTo(type as IType) as bool
		require
			.compiler
		body
			# TODO: some code dup with IType
			if .serialNum==type.serialNum
				assert Object.referenceEquals(this, type)
			if this is type
				return true
			if type is .compiler.passThroughType
				return true
			if type is .compiler.dynamicType
				return true
			if type is .compiler.libraryType('System', 'Object')
				return true
			# TODO: remove this:
			if type inherits GenericParam
				return true
			# TODO: would this be needed if not for qualified types?
			if type inherits WrappedType
				if not type.didBindInt  # TODO: shouldn't need this
					type.bindInt()
				return .isAssignableTo(type.theWrappedType to passthrough)  # CC: weird cast
			result_ = .isDescendantOf(type) # CC: rename to result
			# TODO: lameness follows. all revolves around generics and constructed types
			if not result_
				if .name==type.name
					# Tests\400-generics\100-use-generics-collections\320-pass-generic-return-value.cobra
					result_ = true
				else if this inherits Class and type inherits Interface and .isGeneric and type.isGeneric
					if .name.toLower() in type.name.toLower()
						# \Tests\400-generics\100-use-generics-collections\300-use-ilist.cobra
						result_ = true
				if false
					print
					print '<> isAssignableTo'
					print '...this = ' stop
					.writeDeepString()
					print '...type = ' stop
					type.writeDeepString()
			return result_

	# CC: duplicated from Type
	def isComparableTo(t as IType) as bool
		if t inherits NilableType
			# CC: t = t.theWrappedType
			$sharp('t = ((NilableType)t).TheWrappedType')
		compareTo = .memberForName('compareTo')
		if compareTo and compareTo.isMethod and compareTo.resultType is .compiler.intType and t and t.isDescendantOf(this)
			# TODO: check that the compareTo can take a `t` as an argument
			return true
		if t inherits NilType  # TODO: is this needed? See comment in isEquatableTo
			return true
		return false

	# CC: duplicated from Type
	def isEquatableTo(t as IType) as bool
		if t inherits NilableType
			# CC: b = b.theWrappedType
			$sharp('t = ((NilableType)t).TheWrappedType')
		if .isAssignableTo(t to IType) or t.isAssignableTo(this)  # CC: to ! or assert above with nil flow analysis
			return true
		if t inherits NilType # TODO: axe this. but temp reason for this is due to Tests\150-basics-two\100-using.cobra
			return true
		return false

	def isDescendantOf(type as IType) as bool
		return type is this

	get isDynamic as bool
		return false

	get isReference as bool
		throw OverrideException(.getType)
		return false

	get innerType as IType?
		return nil

	get realType as IType
		return this
		
	get superType as IType?
		require .didBindInt
		return nil

	get genericTypeDef as IType?
		return nil
		
	get isGenericDef as bool
		return false

	get isGeneric as bool
		return false

	get genericParams as List<of IType>
		if .isGenericDef
			throw Exception('.isGenericDef returns true, but there is no override to return the genericParams')
		else
			throw Exception('.isGenericDef returns false, so there can be no genericParams')

	def constructedTypeFor(typeArgs as List<of IType>) as IType
		require  # TODO: make this stronger by pushing up to base. # TODO: inherit this from the interface instead
			.isGeneric
			.isGenericDef
		body
			if .isGenericDef
				throw Exception('.isGenericDef returns true, but there is no override to constructedTypeFor')
			else
				throw Exception('.isGenericDef returns false, so there can be no constructedTypeFor')
			return CobraType()  # TODO: axe whole line when code flow analysis proves it's not needed

	def secondaryConstructedTypeFor(box as Box, gpToType as Dictionary<of GenericParam, IType>) as IType
		return this

	def isConstructionOf(type as IType) as bool
		assert type.isGenericDef  # TODO: make a requirement of the interface
		if .genericTypeDef is nil
			return false
		if .genericTypeDef is type
			return true
		if .genericTypeDef.genericTypeDef
			return .genericTypeDef.isConstructionOf(type) # TODO: would iteration be more efficient here?
		return false

	def memberForName(name as String) as IMember?
		return .declForName(name)

	get csInit as String
		throw OverrideException(.getType)
		return ''

	# TODO: _isNames, writeSharpIsNames() and defaultAccessLevel should be moved out to a mixin since NameSpace, a subclass of Container, has no isNames. Box and Enum do. So does ClassMember which duplicates this.

	get defaultAccessLevel as String
		throw OverrideException(.getType)
		return ''

	def bindInh
		pass

	get isShared as bool
		return 'shared' in _isNames

	def unNilReturnType
		# TODO: can this be axed when IMember gets broken up?
		pass

	get sharpNameComponent as String
		"""
		Returns a string that can be used in a C# identifier. That means it cannot have special symbols such as period or left bracket.
		"""
		ensure result.length > 0  # TODO? Move to interface
		return .sharpRef

	var _sharpRef as String?

	get sharpRef as String is override
		if _sharpRef is nil and .didBindImp
			_sharpRef = _computeSharpRef
			return _sharpRef to String # CC: axe cast
		else
			return _computeSharpRef
	
	get _computeSharpRef as String
		if .parent
			s = .parent.sharpRef
			if s.length
				s += '.'
			s += .sharpName
		else
			s = .sharpName
		return s

	def writeSharpIsNames(sw as SharpWriter)
		# TODO: cache this somewhere
		accessLevels = ['public', 'protected', 'internal', 'protected internal', 'private']
		# CC: accessLevels = 'public,protected,internal,protected internal,private'.split(c',')
			# same names in both Cobra and C#

		# TODO:
		# if isNames is nil
		#	isNames = _isNames
		isNames = List<of String>()
		isNames.addRange(_isNames)
		if .defaultAccessLevel
			found = false
			for level as String in accessLevels  # CC: axe as
				if level in isNames
					found = true
			if not found
				isNames.insert(0, .defaultAccessLevel)
		isNameCS = {
			# only have to specify the ones that are different
			'shared': 'static',
			'nonvirtual': '',
		}
		sep = ''
		for name in isNames
			name = Utils.getSS(isNameCS to passthrough, name, name) to String # CC: to !
			sw.write(sep)
			sw.write(name)
			sep = ' '
		if sep.length
			sw.write(' ')

	def writeSharpTestInvocation(sw as SharpWriter)
		pass


	## Other

	def cloneCollections
		_declsInOrder = List<of IMember>(_declsInOrder)
		_declsByName = Dictionary<of String, IMember>(_declsByName)
		_declsByNameCI = Dictionary<of String, IMember>(_declsByNameCI)


interface IMember
	inherits INamedNode
	"""
	TODO: this really needs to be split up into INameSpaceMember and IBoxMember. *Maybe* there is a logical IMember parent interface to both of those.
	
	Anything that could conceivably be a member of namespace, class,
	struct or interface, implements IMember.

	That includes namespaces, classes, structs and interfaces
	themselves. As well as the obvious choices of methods, constructors,
	properties and indexers. Also, enums and generic parameters, which
	themselves can be specific types such as `int`.

	That mostly leaves statements and expressions as the nodes that
	cannot be IMembers.

	IContainer and Container both reference IMember extensively.

	Furthermore, IContainer happens to implement IMember because, as it
	turns out, all of the containers may be members of other containers
	(though not without restrictions (a namespace can belong to only
	another namespace)).
	"""

	get isCallable as bool
		"""
		Returns true if this member can be called with parens: () or (args)
		"""

	get isShared as bool
		"""
		Returns true if this member is shared (non-shared members can be
		called instance members).
		"""

	get englishName as String  # TODO: rename to englishTypeName
		"""
		Return the type of member as a plain, lowercase English word:
			class, property, method, namespace, etc.
		"""

	pro parentNameSpace as NameSpace?
		"""
		The NameSpace that the member directly belongs to, if any.
		"""

	get requiresThis as bool
		"""
		Returns true if accessing the member requires a `this`
		reference. True of non-shared properties, methods and fields.
		"""

	get resultType as IType  # TODO: rename to dottedAccessResultType
		"""
		Returns the result of accessing this member in the source
		expression "foo.bar" where this is the bar. For a method or
		property, this would be the return type. For other members like
		enums and classes, this would be the member itself.
		"""

	def bindInh
		"""
		Bind inheritance.
		"""

	def unNilReturnType
		"""
		This is invoked to fix up the CLR library which does not indicate, for example, that StringBuilder.toString returns String not String?
		"""

	def writeSharpDef(sw as SharpWriter)
		"""
		Write the C# code for this member declaration to the given
		SharpWriter.
		"""

	def writeSharpTestInvocation(sw as SharpWriter)
		"""
		Write the C# call to the test method for this member.
		"""
